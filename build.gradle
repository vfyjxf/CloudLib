import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '0.1.126'
    id "io.github.p03w.machete" version "2.+"
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
}

version = project.mod_version
group = project.maven_group

static def isJetBrainsRuntime() {
    return System.getProperty('java.vm.vendor').contains('JetBrains')
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
compileKotlin.compilerOptions.jvmTarget = JvmTarget.JVM_21

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            gameDirectory = project.file('run-data')

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG

            if (isJetBrainsRuntime()) {
                jvmArgument "-XX:+AllowEnhancedClassRedefinition"
                jvmArgument "-XX:HotswapAgent=fatjar"
            }

        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }

    unitTest {
        enable()
        testedMod = mods."${mod_id}"
    }
}

sourceSets {
    main {
        resources.srcDir 'src/generated/resources'
    }
}

repositories {
    mavenCentral()
    maven { url = "https://maven.quiltmc.org/repository/release/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
    maven { url = "https://maven.blamejared.com/" }
    maven { url = "https://modmaven.dev" }
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://maven.shedaniel.me" }
    maven { url = "https://repo.kenzie.mx/releases" }
    maven { url = "https://maven.tterrag.com/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = 'https://thedarkcolour.github.io/KotlinForForge/' }
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
    localRuntime "maven.modrinth:jei:${jei_version}-neoforge"
    implementation 'thedarkcolour:kotlinforforge-neoforge:5.3.0'
    clientAdditionalRuntimeClasspath(implementation("org.eclipse.collections:eclipse-collections-api:11.1.0"))
    jarJar "org.eclipse.collections:eclipse-collections-api:[11.1.0,)"
    clientAdditionalRuntimeClasspath(implementation("org.eclipse.collections:eclipse-collections:11.1.0"))
    jarJar "org.eclipse.collections:eclipse-collections:[11.1.0,)"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            jei_version            : jei_version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

java {
    withSourcesJar()
}

//tasks.jar.configure {
//    archiveClassifier.set('slim')
//}
//
//tasks.jarJar.configure {
//    archiveClassifier.set('')
//}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.mod_id
            version = project.mod_version
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}