plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.72'
//    id("net.ltgt.errorprone") version "4.1.0"
//    id("net.ltgt.nullaway") version "2.1.0"
}

version = project.mod_version
group = project.maven_group

static def isJetBrainsRuntime() {
    return System.getProperty('java.vm.vendor').contains('JetBrains')
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            gameDirectory = project.file('run-client')
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            gameDirectory = project.file('run-server')
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            gameDirectory = project.file('run-data')
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
            if (isJetBrainsRuntime()) {
                jvmArgument "-XX:+AllowEnhancedClassRedefinition"
                jvmArgument "-XX:HotswapAgent=fatjar"
            }
            systemProperty('terminal.jline', 'true')
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }

    unitTest {
        enable()
        testedMod = mods."${mod_id}"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = "https://maven.quiltmc.org/repository/release/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
    maven { url = "https://maven.blamejared.com/" }
    maven { url = "https://modmaven.dev" }
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://maven.shedaniel.me" }
    maven { url = "https://repo.kenzie.mx/releases" }
    maven { url = "https://maven.tterrag.com/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://maven.minecraftforge.net" }
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
//    errorprone "com.uber.nullaway:nullaway:0.12.6"
//    errorprone "com.google.errorprone:error_prone_core:2.37.0"
//    compileOnly "net.minecraftforge:eventbus:7.0-beta.1"
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
    additionalRuntimeClasspath(implementation("org.eclipse.collections:eclipse-collections-api:11.1.0"))
    jarJar("org.eclipse.collections:eclipse-collections-api:[11.1.0,)") {
        version {
            prefer("11.1.0")
        }
    }
    additionalRuntimeClasspath(implementation("org.eclipse.collections:eclipse-collections:11.1.0"))
    jarJar("org.eclipse.collections:eclipse-collections:[11.1.0,)") {
        version {
            prefer("11.1.0")
        }
    }
    additionalRuntimeClasspath(implementation 'org.appliedenergistics.yoga:yoga:1.0.0')
    jarJar("org.appliedenergistics.yoga:yoga:1.0.0") {
        version {
            prefer("1.0.0")
        }
    }
    additionalRuntimeClasspath(implementation("net.lenni0451:Reflect:1.4.0"))
    jarJar "net.lenni0451:Reflect:1.4.0"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation "net.neoforged:testframework:${project.neo_version}"
    testImplementation 'com.palantir.javapoet:javapoet:0.6.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
}

test {
    useJUnitPlatform()
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            jei_version            : jei_version
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

java {
    withSourcesJar()
}
// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.mod_id
            version = project.mod_version
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}